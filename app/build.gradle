apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

def versionMajor = 1
def versionMinor = 1
def versionPatch = 5

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "fr.paug.androidmakers"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch * 10
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions 'environment'

    productFlavors {
        prod {
            dimension 'environment'
        }

        preprod {
            dimension 'environment'
            applicationIdSuffix ".preprod"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Rename output apk (Just for manual signing)
            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def formattedDate = new Date().format('yyyy-MM-dd_HH-mm-ss')
                    def newName = output.outputFile.name
                    newName = newName.replace("app", "AndroidMakers")
                    newName = newName.replace("-release", "-release-" + versionName + "-" + versionCode + "-" + formattedDate)
                    outputFileName = newName
                }
            }
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    // Support
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:support-vector-drawable:$support_version"
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:percent:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"
    implementation "com.android.support:support-emoji:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    // Firebase
    implementation "com.google.firebase:firebase-core:$google_play_services_client_library_version"
    implementation "com.google.firebase:firebase-database:$google_play_services_client_library_version"
    implementation "com.google.firebase:firebase-messaging:$google_play_services_client_library_version"
    implementation 'com.firebase:firebase-jobdispatcher:0.8.5'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.1'

    // Image management
    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    // Used for tags
    implementation 'org.apmem.tools:layouts:1.10@aar'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

}

apply plugin: 'com.google.gms.google-services'